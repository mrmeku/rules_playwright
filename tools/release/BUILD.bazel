load("@aspect_bazel_lib//lib:copy_to_directory.bzl", "copy_to_directory")
load("@aspect_bazel_lib//lib:write_source_files.bzl", "write_source_file")
load("@bazel_skylib//rules:native_binary.bzl", "native_binary")

package(default_visibility = ["//visibility:public"])

config_setting(
    name = "release",
    values = {
        "compilation_mode": "opt",
    },
)

[
    platform(
        name = "{}_{}".format(os, cpu),
        constraint_values = [
            "@platforms//os:" + os,
            "@platforms//cpu:" + cpu,
            "//tools/linkers:musl" if os == "linux" else "//tools/linkers:unknown",
        ],
    )
    for os in [
        "linux",
        "macos",
    ]
    for cpu in [
        "arm64",
        "x86_64",
    ]
]

LINUX_ARTIFACTS = [
    "//playwright/private/cli:cli_linux",
]

MACOS_ARTIFACTS = [
    "//playwright/private/cli:cli_macos",
]

copy_to_directory(
    name = "release_artifacts",
    srcs = LINUX_ARTIFACTS + MACOS_ARTIFACTS,
    out = "artifacts",
    root_paths = ["playwright/private/cli"],
    tags = ["manual"],
)

write_source_file(
    name = "copy_release_artifacts",
    diff_test = False,
    executable = True,
    in_file = ":release_artifacts",
    out_file = "artifacts",
    tags = ["manual"],
)

native_binary(
    name = "cli",
    src = select(
        {
            "//tools/platforms:macos_x86_64": ":artifacts/cli-x86_64-apple-darwin",
            "//tools/platforms:macos_arm64": ":artifacts/cli-arm64-apple-darwin",
            "//tools/platforms:linux_x86_64": ":artifacts/cli-x86_64-unknown-linux-musl",
            "//tools/platforms:linux_arm64": ":artifacts/cli-arm64-unknown-linux-musl",
        },
    ),
    out = "cli",
)
