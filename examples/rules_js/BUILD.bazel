load("@npm//:@playwright/test/package_json.bzl", playwright_bin = "bin")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("@npm//:http-server/package_json.bzl", http_server_bin = "bin")

npm_link_all_packages(name = "node_modules")

filegroup(
    name = "todomvc_files",
    srcs = ["todomvc"],
    visibility = ["//visibility:public"],
)

DATA = [
    "package.json",
    "playwright.config.ts",
    "tests/example.spec.ts",
    "//:node_modules/@playwright/test",
    ":test-server",
]

http_server_bin.http_server_binary(
    name = "test-server",
    copy_data_to_bin = False,
    data = [
        "//:todomvc_files",
    ],
    fixed_args = [
        "$(rootpath //:todomvc_files)",
        "-p 1234",
    ],
)

playwright_bin.playwright_test(
    name = "test",
    args = [
        "test",
    ],
    data = DATA + [
        "@playwright//:chromium-headless-shell",
        "@playwright//:firefox",
        "@playwright//:webkit",
    ],
    env = {
        "CI": "1",
        "TEST_SERVER": "$(rootpath :test-server)",
        "PLAYWRIGHT_BROWSERS_PATH": "$(rootpath @playwright//:chromium-headless-shell)/../",
    },
    tags = [
        # Firefox does not run on mac with sandboxing
        "no-sandbox",
    ],
)

playwright_bin.playwright_binary(
    name = "ui",
    args = [
        "test",
        "--project=chromium",
        "--ui",
    ],
    data = DATA + [
        "@playwright//:chromium",
        "@playwright//:chromium-headless-shell",
    ],
    env = {
        "TEST_SERVER": "$(rootpath :test-server)",
        "PLAYWRIGHT_BROWSERS_PATH": "$(rootpath @playwright//:chromium)/../",
    },
)
